plugins {
    id 'java'
    id "com.gradleup.shadow" version "8.3.6"
}

group = 'com.epicplayera10'
version = '1.1.0'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }

    // FAWE
    maven { url = uri("https://maven.enginehub.org/repo/") }

    // ItemsAdder
    maven {
        url 'https://maven.devs.beer/'
    }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.21.4-R0.1-SNAPSHOT"

    //compileOnly("com.sk89q.worldedit:worldedit-core:7.3.0")
    //compileOnly("com.sk89q.worldedit:worldedit-bukkit:7.3.0")

    implementation(platform("com.intellectualsites.bom:bom-newest:1.52"))
    compileOnly("com.fastasyncworldedit:FastAsyncWorldEdit-Core:2.13.0")
    compileOnly("com.fastasyncworldedit:FastAsyncWorldEdit-Bukkit:2.13.0") { transitive = false }

    compileOnly 'dev.lone:api-itemsadder:4.0.10'

    implementation "org.bstats:bstats-bukkit:3.0.2"
}

shadowJar {
    archiveFileName = "${project.name}-${project.version}.jar"

    relocate("org.bstats", "com.epicplayera10.iaedit.libs.org.bstats")
}

assemble.dependsOn(shadowJar)

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
